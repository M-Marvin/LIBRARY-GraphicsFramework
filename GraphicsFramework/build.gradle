import org.gradle.internal.os.OperatingSystem

apply plugin: 'java-library'
apply plugin: 'maven-publish'

project.ext.lwjglVersion = "3.3.3"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
			? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
			: "natives-windows-x86"
		break
}

version = '1.4.3'

repositories {
	mavenCentral()
	maven {
		url 'https://maven.pkg.github.com/m-marvin/library-simplelogging'
		credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
	}
	maven {
		url 'https://maven.pkg.github.com/m-marvin/library-unifiedvectors'
		credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
	}
	mavenLocal()
}

dependencies {
	
	// LWJGL
	api platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	api "org.lwjgl:lwjgl"
	api "org.lwjgl:lwjgl-glfw"
	api "org.lwjgl:lwjgl-openal"
	api "org.lwjgl:lwjgl-opengl"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	
	// UniVec
	api 'de.m_marvin.univec:univec:1.6.1'
	
	// GSON
	api "com.google.code.gson:gson:2.9.1"
	
	// SimpleLogging
	api 'de.m_marvin.simplelogging:simplelogging:2.1.1'
	
	// Physic Engine (Testing)
	testImplementation "cz.advel.jbullet:jbullet:20101010-1"
	
	// Quickhull (Testing)
	testImplementation "com.github.quickhull3d:quickhull3d:1.0.0"
	
}

java {
    withSourcesJar()
	sourceCompatibility = "1.17"
	targetCompatibility = "1.17"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/m-marvin/library-graphicsframework")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'de.m_marvin.gframe'
        	artifactId = "gframe"
        
			from components.java
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Vendor-Id': 'de.m_marvin'
    }
}
