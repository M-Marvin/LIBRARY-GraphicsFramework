import org.gradle.internal.os.OperatingSystem

apply plugin: 'java-library'
apply plugin: 'maven-publish'

project.ext.lwjglVersion = "3.3.3"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
			? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
			: "natives-windows-x86"
		break
}

group = 'com.github.M-Marvin'
version = '0.4'

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
	
	// LWJGL
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	api "org.lwjgl:lwjgl"
	api "org.lwjgl:lwjgl-glfw"
	api "org.lwjgl:lwjgl-openal"
	api "org.lwjgl:lwjgl-opengl"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	
	// UniVec
	api 'com.github.M-Marvin:LIBRARY-UnifiedVectors:main-SNAPSHOT'
	
	// GSON
	api "com.google.code.gson:gson:2.9.1"
	
	// SimpleLogging
	api 'com.github.M-Marvin:LIBRARY-SimpleLogging:main-SNAPSHOT'
	
	// CommandLineParser (Testing)
	//implementation 'com.github.M-Marvin:LIBRARY-CommandLineParser:main-SNAPSHOT'
	
	// Physic Engine (Testing)
	testImplementation "cz.advel.jbullet:jbullet:20101010-1"
	
}

java {
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
			from components.java
        }
    }
}

// Specify gradle version to use for jitpack
wrapper {
    gradleVersion = "7.4.2"
    distributionType = Wrapper.DistributionType.ALL
}

jar {
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Implementation-Title': 'RenderEngine'
        attributes 'Implementation-Vendor-Id': 'de.m_marvin'
    }
}
